buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

subprojects {
    apply plugin: 'eclipse-wtp'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports { mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}" }
    }

    // add SpringLoaded for all sub-projects which are SpringBoot enabled
    // using name matching (instead of withType()) to overcome classloader issues
    // @see https://github.com/gradle/gradle-native/issues/742
    plugins.matching { it.class.simpleName.endsWith("SpringBootPlugin") }.configureEach { bootProject ->
        if (hasProperty('springLoaded')) {
            bootRun.jvmArgs += springLoaded
            bootRun.jvmArgs += "-noverify"
        }
        dependencies { compile group: "com.vaadin", name: "vaadin-spring-boot-starter" }
    }
}